//
// Usage:
// Apply this gradle config file.
//   apply from: 'https://raw.githubusercontent.com/hata/gradle-plugins/master/embulk-integration-test.gradle'
// Set dependency like this:
//   project.tasks.integrationTest.dependsOn(classpath)
//
// To disable this plugin
// ./gradlew -DskipIntegrationTest=true gem
//

apply from: 'https://raw.github.com/brunodecarvalho/gradle-plugins/master/integration-test.gradle'

// TODO: Can I override these properties ?
def embulkDownloadURL = "http://dl.embulk.org/embulk-latest.jar"
def embulkExecPath = ''
def skipIntegrationTestFlag = Boolean.getBoolean('skipIntegrationTest')
def embulkIntegrationTestDir = 'embulk.integrationtest.dir'

task downloadEmbulk {
  embulkExecPath = "$temporaryDir/embulk/bin/embulk"

  doFirst {
    if (skipIntegrationTestFlag) {
      throw new StopExecutionException('Skip downloadEmbulk task.')
    }
  }

  outputs.upToDateWhen {
    new File(embulkExecPath).exists()
  }

  doLast {
    exec {
      executable "sh"
      args "-c", "curl --create-dirs -o $embulkExecPath -L $embulkDownloadURL"
    }
    exec {
      executable "sh"
      args "-c", "chmod +x $embulkExecPath"
    }
  }
}


integrationTest.doFirst {
  String libDir = file('lib').absolutePath
  String baseDir = "$temporaryDir/embulk"
  systemProperty "$embulkIntegrationTestDir", "$baseDir"

  if (skipIntegrationTestFlag) {
    throw new StopExecutionException('Skip integrationTest task.')
  }

  copy {
    from 'src/integration-test/resources'
    into baseDir
  }

  fileTree(dir: "$baseDir", include: "*.yml").visit { config ->
    ['preview', 'run'].each { embulkCommand ->
      exec {
        workingDir baseDir
        executable "sh"
        args "-c", "$embulkExecPath $embulkCommand -I $libDir $config.file.absolutePath --log ${config.file.absolutePath}.${embulkCommand}.log"
      }
    }
  }
}

project.tasks.integrationTest.dependsOn(downloadEmbulk)

